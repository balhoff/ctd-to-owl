PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX acts_upstream_of: <http://purl.obolibrary.org/obo/RO_0002263>
PREFIX auo_pe: <http://purl.obolibrary.org/obo/RO_0004034>
PREFIX auo_ne: <http://purl.obolibrary.org/obo/RO_0004035>
PREFIX Gene: <http://purl.obolibrary.org/obo/SO_0000704>
PREFIX Chemical: <http://purl.obolibrary.org/obo/CHEBI_24431>
PREFIX causally_upstream_of: <http://purl.obolibrary.org/obo/RO_0002411>
PREFIX causally_upstream_pos: <http://purl.obolibrary.org/obo/RO_0002304>
PREFIX causally_upstream_neg: <http://purl.obolibrary.org/obo/RO_0002305>
PREFIX participates_in: <http://purl.obolibrary.org/obo/RO_0000056>
PREFIX has_input: <http://purl.obolibrary.org/obo/RO_0002233>
PREFIX enabled_by: <http://purl.obolibrary.org/obo/RO_0002333>
PREFIX GeneExpression: <http://purl.obolibrary.org/obo/GO_0010467>
PREFIX CoTreatment: <http://purl.obolibrary.org/obo/CTDI_26>
PREFIX ResponseToChemical: <http://purl.obolibrary.org/obo/GO_0042221>
PREFIX MetabolicProcess: <http://purl.obolibrary.org/obo/GO_0008152>
PREFIX MolecularFunction: <http://purl.obolibrary.org/obo/GO_0003674>
PREFIX Methylation: <http://purl.obolibrary.org/obo/GO_0032259>
PREFIX Transport: <http://purl.obolibrary.org/obo/GO_0006810>
PREFIX ImportIntoCell: <http://purl.obolibrary.org/obo/GO_0098657>
PREFIX Cleavage: <http://purl.obolibrary.org/obo/CTDI_8>
PREFIX Oxidation: <http://purl.obolibrary.org/obo/CTDI_20>
PREFIX ChemicalSynthesis: <http://purl.obolibrary.org/obo/CTDI_10>
PREFIX Phosphorylation: <http://purl.obolibrary.org/obo/GO_0016310>
PREFIX Localization: <http://purl.obolibrary.org/obo/GO_0051179>
PREFIX Secretion: <http://purl.obolibrary.org/obo/GO_0046903>
PREFIX RNASplicing: <http://purl.obolibrary.org/obo/GO_0008380>
PREFIX ProteinOLinkedGlycosylation: <http://purl.obolibrary.org/obo/GO_0006493>
PREFIX Binding: <http://purl.obolibrary.org/obo/GO_0005488>
PREFIX Reduction: <http://purl.obolibrary.org/obo/CTDI_21>
PREFIX Abundance: <http://purl.obolibrary.org/obo/CTDI_1>
PREFIX Mutagenesis: <http://purl.obolibrary.org/obo/CTDI_19>
PREFIX Uptake: <http://purl.obolibrary.org/obo/CTDI_25>
PREFIX CatabolicProcess: <http://purl.obolibrary.org/obo/GO_0009056>
PREFIX ProteinGlucuronidation: <http://purl.obolibrary.org/obo/GO_0018411>
PREFIX Stability: <http://purl.obolibrary.org/obo/CTDI_24>
PREFIX Acetylation: <http://purl.obolibrary.org/obo/CTDI_2>
PREFIX Folding: <http://purl.obolibrary.org/obo/CTDI_13>
PREFIX ProteinUbiquitination: <http://purl.obolibrary.org/obo/GO_0016567>
PREFIX ProteinNitrosylation: <http://purl.obolibrary.org/obo/GO_0017014>
PREFIX Alkylation: <http://purl.obolibrary.org/obo/CTDI_5>
PREFIX ProteinSumoylation: <http://purl.obolibrary.org/obo/GO_0016925>
PREFIX ProteinPrenylation: <http://purl.obolibrary.org/obo/GO_0018342>
PREFIX Glycation: <http://purl.obolibrary.org/obo/CTDI_15>
PREFIX Hydroxylation: <http://purl.obolibrary.org/obo/CTDI_16>
PREFIX has_form: <http://ctd.example.org/has_form>
PREFIX occurs_in: <http://purl.obolibrary.org/obo/BFO_0000066>

SELECT DISTINCT (REPLACE(STR(?chem_type), "http://id\\.nlm\\.nih\\.gov/mesh/", "mesh:") AS ?chemicalID) (STR(?chem_type_label) AS ?chem_label) ?interaction ?direction (REPLACE(STR(?gene_type), "http://identifiers\\.org/ncbigene:", "ncbigene:") AS ?geneID) (STR(?gene_type_label) AS ?gene_label) (STR(?form_typed) AS ?form) (REPLACE(STR(?taxon), "http://purl\\.obolibrary\\.org/obo/NCBITaxon_", "ncbitaxon:") AS ?taxonID) (REPLACE(STR(?pmid), "https://www\\.ncbi\\.nlm\\.nih\\.gov/pubmed/", "pmid:") AS ?PMID)
WHERE {
  ?chem participates_in: ?chem_process .
  ?chem_process ?rel ?process .
  FILTER (?rel IN (causally_upstream_of:, causally_upstream_pos:, causally_upstream_neg:))
  ?process has_input:|enabled_by: ?gene .
  ?process rdf:type ?process_type .
  # TODO get this from reasoning?
  ?x causally_upstream_of:? ?process .
  ?x occurs_in: / rdf:type ?taxon .
  ?x dc:source ?pmid .
  ?gene_type rdfs:label ?gene_type_label .
  ?gene has_form: ?form_typed .
  ?gene rdf:type Gene: .
  ?gene rdf:type ?gene_type .
  ?chem rdf:type Chemical: .
  ?chem rdf:type ?chem_type .
  ?chem_type rdfs:label ?chem_type_label .
  VALUES ?direction { "->" }
  VALUES (?rel ?process_type ?interaction) {
    (causally_upstream_of: GeneExpression: "affects expression of")
    (causally_upstream_pos: GeneExpression: "increases expression of")
    (causally_upstream_neg: GeneExpression: "decreases expression of")
    (causally_upstream_of: Methylation: "affects molecular modification of")
    (causally_upstream_pos: Methylation: "increases molecular modification of")
    (causally_upstream_neg: Methylation: "decreases molecular modification of")
    (causally_upstream_of: MolecularFunction: "affects activity of")
    (causally_upstream_pos: MolecularFunction: "increases activity of")
    (causally_upstream_neg: MolecularFunction: "decreases activity of")
    (causally_upstream_of: ResponseToChemical: "affects response to")
    (causally_upstream_pos: ResponseToChemical: "increases response to")
    (causally_upstream_neg: ResponseToChemical: "decreases response to")
    (causally_upstream_of: Phosphorylation: "affects molecular modification of")
    (causally_upstream_pos: Phosphorylation: "increases molecular modification of")
    (causally_upstream_neg: Phosphorylation: "decreases molecular modification of")
    (causally_upstream_of: Binding: "molecularly interacts with")
    (causally_upstream_pos: Binding: "increases molecular interaction with")
    (causally_upstream_neg: Binding: "decreases molecular interaction with")
    (causally_upstream_of: MetabolicProcess: "affects metabolic processing of")
    (causally_upstream_pos: MetabolicProcess: "increases metabolic processing of")
    (causally_upstream_neg: MetabolicProcess: "decreases metabolic processing of")
    (causally_upstream_of: Transport: "affects transport of")
    (causally_upstream_pos: Transport: "increases transport of")
    (causally_upstream_neg: Transport: "decreases transport of")
    (causally_upstream_of: ImportIntoCell: "affects uptake of")
    (causally_upstream_pos: ImportIntoCell: "increases uptake of")
    (causally_upstream_neg: ImportIntoCell: "decreases uptake of")
    (causally_upstream_of: Cleavage: "affects degradation of")
    (causally_upstream_pos: Cleavage: "increases degradation of")
    (causally_upstream_neg: Cleavage: "decreases degradation of")
    (causally_upstream_of: Oxidation: "affects molecular modification of")
    (causally_upstream_pos: Oxidation: "increases molecular modification of")
    (causally_upstream_neg: Oxidation: "decreases molecular modification of")
    (causally_upstream_of: ChemicalSynthesis: "affects synthesis of")
    (causally_upstream_pos: ChemicalSynthesis: "increases synthesis of")
    (causally_upstream_neg: ChemicalSynthesis: "decreases synthesis of")
    (causally_upstream_of: Localization: "affects localization of")
    (causally_upstream_pos: Localization: "increases localization of")
    (causally_upstream_neg: Localization: "decreases localization of")
    (causally_upstream_of: Secretion: "affects secretion of")
    (causally_upstream_pos: Secretion: "increases secretion of")
    (causally_upstream_neg: Secretion: "decreases secretion of")
    (causally_upstream_of: RNASplicing: "affects splicing of")
    (causally_upstream_pos: RNASplicing: "increases splicing of")
    (causally_upstream_neg: RNASplicing: "decreases splicing of")
    (causally_upstream_of: ProteinOLinkedGlycosylation: "affects molecular modification of")
    (causally_upstream_pos: ProteinOLinkedGlycosylation: "increases molecular modification of")
    (causally_upstream_neg: ProteinOLinkedGlycosylation: "decreases molecular modification of")
    (causally_upstream_of: Reduction: "affects molecular modification of")
    (causally_upstream_pos: Reduction: "increases molecular modification of")
    (causally_upstream_neg: Reduction: "decreases molecular modification of")
    (causally_upstream_of: Abundance: "affects abundance of")
    (causally_upstream_pos: Abundance: "increases abundance of")
    (causally_upstream_neg: Abundance: "decreases abundance of")
    (causally_upstream_of: Mutagenesis: "affects mutation rate of")
    (causally_upstream_pos: Mutagenesis: "increases mutation rate of")
    (causally_upstream_neg: Mutagenesis: "decreases mutation rate of")
    (causally_upstream_of: Uptake: "affects uptake of")
    (causally_upstream_pos: Uptake: "increases uptake of")
    (causally_upstream_neg: Uptake: "decreases uptake of")
    (causally_upstream_of: CatabolicProcess: "affects degradation of")
    (causally_upstream_pos: CatabolicProcess: "increases degradation of")
    (causally_upstream_neg: CatabolicProcess: "decreases degradation of")
    (causally_upstream_of: ProteinGlucuronidation: "affects molecular modification of")
    (causally_upstream_pos: ProteinGlucuronidation: "increases molecular modification of")
    (causally_upstream_neg: ProteinGlucuronidation: "decreases molecular modification of")
    (causally_upstream_of: Stability: "affects stability of")
    (causally_upstream_pos: Stability: "increases stability of")
    (causally_upstream_neg: Stability: "decreases stability of")
    (causally_upstream_of: Acetylation: "affects molecular modification of")
    (causally_upstream_pos: Acetylation: "increases molecular modification of")
    (causally_upstream_neg: Acetylation: "decreases molecular modification of")
    (causally_upstream_of: Folding: "affects folding of")
    (causally_upstream_pos: Folding: "increases folding of")
    (causally_upstream_neg: Folding: "decreases folding of")
    (causally_upstream_of: ProteinUbiquitination: "affects molecular modification of")
    (causally_upstream_pos: ProteinUbiquitination: "increases molecular modification of")
    (causally_upstream_neg: ProteinUbiquitination: "decreases molecular modification of")
    (causally_upstream_of: ProteinNitrosylation: "affects molecular modification of")
    (causally_upstream_pos: ProteinNitrosylation: "increases molecular modification of")
    (causally_upstream_neg: ProteinNitrosylation: "decreases molecular modification of")
    (causally_upstream_of: Alkylation: "affects molecular modification of")
    (causally_upstream_pos: Alkylation: "increases molecular modification of")
    (causally_upstream_neg: Alkylation: "decreases molecular modification of")
    (causally_upstream_of: ProteinSumoylation: "affects molecular modification of")
    (causally_upstream_pos: ProteinSumoylation: "increases molecular modification of")
    (causally_upstream_neg: ProteinSumoylation: "decreases molecular modification of")
    (causally_upstream_of: ProteinPrenylation: "affects molecular modification of")
    (causally_upstream_pos: ProteinPrenylation: "increases molecular modification of")
    (causally_upstream_neg: ProteinPrenylation: "decreases molecular modification of")
    (causally_upstream_of: Glycation: "affects molecular modification of")
    (causally_upstream_pos: Glycation: "increases molecular modification of")
    (causally_upstream_neg: Glycation: "decreases molecular modification of")
    (causally_upstream_of: Hydroxylation: "affects molecular modification of")
    (causally_upstream_pos: Hydroxylation: "increases molecular modification of")
    (causally_upstream_neg: Hydroxylation: "decreases molecular modification of")
  }
  FILTER (?chem_type NOT IN (Chemical:, Gene:, owl:NamedIndividual, owl:Thing, <http://purl.obolibrary.org/obo/BFO_0000002>, <http://purl.obolibrary.org/obo/BFO_0000003>, <http://purl.obolibrary.org/obo/BFO_0000004>, <http://purl.obolibrary.org/obo/BFO_0000015>, <http://purl.obolibrary.org/obo/BFO_0000040>))
  FILTER (?gene_type NOT IN (Chemical:, Gene:, owl:NamedIndividual, owl:Thing, <http://purl.obolibrary.org/obo/BFO_0000002>, <http://purl.obolibrary.org/obo/BFO_0000003>, <http://purl.obolibrary.org/obo/BFO_0000004>, <http://purl.obolibrary.org/obo/BFO_0000015>, <http://purl.obolibrary.org/obo/BFO_0000040>))
  FILTER (?process_type NOT IN (Chemical:, Gene:, owl:NamedIndividual, owl:Thing, <http://purl.obolibrary.org/obo/BFO_0000002>, <http://purl.obolibrary.org/obo/BFO_0000003>, <http://purl.obolibrary.org/obo/BFO_0000004>, <http://purl.obolibrary.org/obo/BFO_0000015>, <http://purl.obolibrary.org/obo/BFO_0000040>))
  FILTER (?taxon NOT IN (Chemical:, Gene:, owl:NamedIndividual, owl:Thing, <http://purl.obolibrary.org/obo/BFO_0000002>, <http://purl.obolibrary.org/obo/BFO_0000003>, <http://purl.obolibrary.org/obo/BFO_0000004>, <http://purl.obolibrary.org/obo/BFO_0000015>, <http://purl.obolibrary.org/obo/BFO_0000040>))
  FILTER (?rel IN (causally_upstream_pos:, causally_upstream_neg:) || NOT EXISTS { ?chem_process causally_upstream_pos:|causally_upstream_neg: ?process })
}
